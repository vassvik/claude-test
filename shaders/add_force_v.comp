#version 430 core

layout(local_size_x = 16, local_size_y = 16) in;

// v-velocity grid: 512x513
layout(r32f, binding = 0) uniform image2D vVelocity;

uniform vec2 point;      // Position of force application (0-1 in cell-center space)
uniform float forceY;    // Force y-component (grid cells/sec)
uniform float radius;    // Radius of influence (in UV space)
uniform ivec2 vSize;     // 512x513

void main() {
    ivec2 pos = ivec2(gl_GlobalInvocationID.xy);

    if (pos.x >= vSize.x || pos.y >= vSize.y) return;

    // v[i,j] lives at horizontal face position (i+0.5, j) in world space
    // Convert to normalized (0-1) cell-center space for distance calculation
    vec2 uv_v = vec2(float(pos.x) + 0.5, float(pos.y)) / vec2(512.0, 512.0);

    float dist = length(uv_v - point);
    float influence = exp(-dist * dist / (radius * radius));

    float v = imageLoad(vVelocity, pos).r;
    v += forceY * influence;
    v = clamp(v, -3840.0, 3840.0);  // Max 64 cells/step at 60fps
    imageStore(vVelocity, pos, vec4(v, 0.0, 0.0, 0.0));
}
